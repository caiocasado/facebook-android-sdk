apply plugin: 'com.android.library'

repositories {
  mavenCentral()
}

project.group = 'com.facebook.android'

dependencies {
    compile 'com.android.support:support-v4:20.0.+'
    compile 'com.parse.bolts:bolts-android:1.1.2'
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

apply plugin: 'maven'

version = "1.0.4"
group = "com.facebook"

configurations {
    archives {
        extendsFrom configurations.default
    }
}

afterEvaluate { project ->
    uploadArchives {
        configuration = configurations.archives
        repositories.mavenDeployer {

            repository(url: NEXUS_URL) {
                authentication(userName: NEXUS_USERNAME,
                        password: NEXUS_PASSWORD)
            }

            pom.artifactId = "facebook-android-sdk"
            pom.project {
                name 'Facebook SDK for Android'
                packaging 'aar'
                description 'The facebook sdk for android'
                url 'https://github.com/foursquare/facebook-sdk-android'

                scm {
                    url 'scm:git@github.com:foursquare/facebook-sdk-android.git'
                    connection 'scm:git@github.com:foursquare/facebook-sdk-android.git'
                    developerConnection 'scm:git@github.com:foursquare/facebook-sdk-android.git'
                }

                developers {
                    developer {
                        id 'kylefowler'
                        name 'Kyle Fowler'
                        email 'kyle.e.fowler@gmail.com'
                    }
                }
            }
        }
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    artifacts {
        archives androidSourcesJar
        //archives androidJavadocsJar
    }
}